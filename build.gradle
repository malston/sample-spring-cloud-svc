buildscript {
	ext {
		springBootVersion = '1.4.4.RELEASE'
		cfAppVersion = '1.0.7'
	}
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://repo.spring.io/release" }
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("com.github.pivotalservices:ya-cf-app-gradle-plugin:${cfAppVersion}")
		classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.0.1"
	}
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'cf-app'
apply plugin: 'org.sonarqube'

jar { baseName = 'sample-spring-cloud-svc' }
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://repo.spring.io/release" }
	maven { url "https://repo.spring.io/milestone" }
}

test {
	useJUnit { excludeCategories 'org.bk.producer.test.AcceptanceTest', 'org.bk.producer.test.SmokeTest' }
}

apply from: 'gradle/coverage.gradle'
apply from: 'gradle/sonar.gradle'

dependencies {
	//	compile('io.pivotal.spring.cloud:spring-cloud-services-starter-circuit-breaker')
	compile('io.pivotal.spring.cloud:spring-cloud-services-starter-config-client')
	compile('io.pivotal.spring.cloud:spring-cloud-services-starter-service-registry')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-hateoas')
	compile('org.springframework.cloud:spring-cloud-starter')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.webjars:bootstrap:3.3.4')
	compile('org.webjars:angularjs:1.3.15')
	compile('org.webjars:angular-ui-router:0.2.13')
	compile('org.webjars:jquery:2.1.3')
	compile('junit:junit')
	testCompile('org.assertj:assertj-core')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:1.3.1.RELEASE"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Camden.SR5"
	}
}

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: "http://10.0.0.218:8081/nexus/content/repositories/releases") {
				authentication(userName: "admin", password: "admin123")
			}
			snapshotRepository(url: 'http://10.0.0.218:8081/nexus/content/repositories/snapshots') {
				authentication(userName: 'admin', password: 'admin123');
			}
			pom.version = "${version}"
			pom.artifactId = "sample-spring-cloud-svc-ci"
			pom.groupId = "org.bk"
		}
	}
}

cfConfig {
	name = "sample-spring-cloud-svc"
	hostName = "sample-spring-cloud-svc"
	filePath = "build/libs/sample-spring-cloud-svc-${version}.jar"

	ccHost = "api.local.pcfdev.io"
	ccUser = "admin"
	ccPassword = "admin"
	org = "pcfdev-org"
	space = "pcfdev-space"
	domain = "local.pcfdev.io"
	path = ""

	buildpack = "https://github.com/cloudfoundry/java-buildpack.git"
	environment = ["JAVA_OPTS": "-Djava.security.egd=file:/dev/./urandom", "SPRING_PROFILES_ACTIVE": "cloud"]
	services  = []
}

task cfAcceptanceTest(type: Test, dependsOn: "cf-get-app-detail")  {
	doFirst() { systemProperty "application.url", "${project.cfConfig.applicationDetail.urls[0]}" }
	useJUnit {
		includeCategories 'org.bk.producer.test.AcceptanceTest'
		excludeCategories 'org.bk.producer.test.SmokeTest'
	}
}

task cfSmokeTest(type: Test, dependsOn: "cf-get-app-detail")  {
	doFirst() { systemProperty "application.url", "${project.cfConfig.applicationDetail.urls[0]}" }
	useJUnit {
		includeCategories 'org.bk.producer.test.SmokeTest'
		excludeCategories 'org.bk.producer.test.AcceptanceTest'
	}
}
